
    model {
    
    ## Priors
    # Regression parameters
    alpha ~ dnorm(0, 0.01)        # Intercept
    beta1 ~ dnorm(0, 0.01)        # Slope effect of antecedent ODBA
    beta2[1] <- 0                 # Set to zero effect firl level of population
    beta3[1] <- 0                 # Set to zero effect firl level of population of antecedent PTF
    
    beta2[2] ~ dnorm(0, 0.01)     # Prior for effects of factor population
    beta3[2] ~ dnorm(0, 0.01)     # Prior for effects of factor population
    
    # Dirichlet prior for daily ODBA weights
    for(j in 1:days){
      delta[j] ~ dgamma(1,1)
    }
    
    for (j in 1:days) {
      weight[j] <- delta[j]/sumD
        for (i in 1:nind) {
      # For each time period into the past, compute weighted ODBA variable
        antX1[j,i] <- weight[j]*odba[i,days-j+1]
        }
    # Reorder weights from recent to older
    weightOrdered[days-j+1] <- weight[j]
    }
    
    # Compute sum of deltas (unnormalized weights)
    sumD <- sum(delta)
    
    # Compute cumulative daily weights
    for (j in 1:days) {
    cum.weight[j] <- sum(weightOrdered[1:j])
    }
    
    # Compute antecedent ODBA by summing weighted ODBA variable over past days
    for (i in 1:nind) {
    antODBA[i] <- sum(antX1[,i])
    }
    
    ## Likelihood
    for (i in 1:nind) {
      defer[i] ~ dbern(p[i])
      logit(p[i]) <- alpha + beta1*antODBA[i] + beta2[pop[i]] + beta3[pop[i]]*antODBA[i]
    }

    }
