
    model {
    
    ## Priors
    # Regression parameters
    for (i in 1:2) {
        beta0[i] ~ dnorm(mu_beta0, tau.alpha)
    }
    mu_beta0 ~ dnorm(0, 1/10)
    tau.alpha ~ dgamma(0.01, 0.01)
    
    beta1 ~ dnorm(0, 0.01)      # Antecedent ODBA
    beta2 ~ dnorm(0, 0.01)      # population
    beta3 ~ dnorm(0, 0.01)      # Antecedent ODBA * population
    
    # Dirichlet prior for daily ODBA weights
    for(j in 1:days){
      delta[j] ~ dgamma(1,1)
    }
    
    for (j in 1:days) {
      weight[j] <- delta[j]/sumD
        for (i in 1:nind) {
      # For each time period into the past, compute weighted ODBA variable
        antX1[j,i] <- weight[j]*odba[i,days-j+1]
        }
    # Reorder weights from recent to older
    weightOrdered[days-j+1] <- weight[j]
    }
    
    # Compute sum of deltas (unnormalized weights)
    sumD <- sum(delta)
    
    # Compute cumulative daily weights
    for (j in 1:days) {
    cum.weight[j] <- sum(weightOrdered[1:j])
    }
    
    # Compute antecedent ODBA by summing weighted ODBA variable over past days
    for (i in 1:nind) {
    antODBA[i] <- sum(antX1[,i])
    }
    
    ## Likelihood
    for (i in 1:nind) {
      defer[i] ~ dbern(p[i])
      logit(p[i]) <- beta0[year[i]] + beta1*antODBA[i] + beta2*pop[i] + beta3*antODBA[i]*pop[i]
    }

    }
