
    model {
    
    ## Priors
    # Regression parameters
    for (i in 1:un.yr) {
        beta0[i] ~ dnorm(0, 0.01)
    }
    beta1 ~ dnorm(0, 0.01)

    # Dirichlet prior for daily ODBA weights
    for(j in 1:max.dur){
      delta[j] ~ dgamma(1,1)
    }
    
    for (j in 1:max.dur) {
      weight[j] <- delta[j]/sumD

        # For each time period into the past, compute weighted PTF variable
        for (i in 1:nind) {
          antX1[j,i] <- weight[j]*ptf[i,max.dur-j+1]
        }
        
      # Reorder weights from recent to older
      weightOrdered[max.dur-j+1] <- weight[j]
    }
    
    # Compute sum of deltas (unnormalized weights)
    sumD <- sum(delta)
    
    # Compute cumulative daily weights
    for (j in 1:max.dur) {
      cum.weight[j] <- sum(weightOrdered[1:j])
    }

    # Compute antecedent PTF by summing weighted PTF variable over past days
    for (i in 1:nind) {
      antPTF[i] <- sum(antX1[,i])
    }

    ## Likelihood
    for (i in 1:nind) {
      ad[i] ~ dbern(p[i])
      logit(p[i]) <- beta0[year[i]] + beta1*antPTF[i] 
    }

}
