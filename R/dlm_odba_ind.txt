
    model {
    
    # Priors
    beta0 ~ dnorm(0, 1/10)        # Intercept

    mu1 ~ dnorm(0, 1/10)          # beta1[1]
    mu2 ~ dnorm(0, 1/10)          # beta2[1]

    eta.p1 ~ dgamma(0.1, 1) # Process variance for covariate 1
    sd.q1 <- 1/sqrt(eta.p1)
    
    eta.p2 ~ dgamma(0.1, 1) # Process variance for covariate 1
    sd.q2 <- 1/sqrt(eta.p2)
    
    tau.o ~ dgamma(0.1, 1)
    sd.r <- 1/sqrt(tau.o)
    
    # Initialize (fill in first value)
    beta1[1] <- mu1
    beta2[1] <- mu2

    predY[1] <- beta0 + beta1[1]*prcp[1] + beta2[1]*temp[1]
    y[1] ~ dnorm(predY[1], tau.o)

    # Likelihood (loop over each day)
    for (t in 2:dur) {

      # Process model
      predX1[t] <- 1*beta1[t-1]
      beta1[t] ~ dnorm(predX1[t], eta.p1)	# coefficients for precipitation rate

      predX2[t] <- 1*beta2[t-1]
      beta2[t] ~ dnorm(predX2[t], eta.p2)	# coefficiencts for temperature

      # Observation model
      predY[t] <- beta0 + beta1[t]*prcp[t] + beta2[t]*temp[t]
      y[t] ~ dnorm(predY[t], tau.o)	      # Observation variation
    }
    
    }
