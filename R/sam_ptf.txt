
    model {
    
    ## Priors
    # Random year effect
    # for (i in 1:2) {
    #     beta0[i] ~ dnorm(mu_beta0, tau.beta0)
    # }
    # mu_beta0 ~ dnorm(0, 1/5)
    # tau.beta0 ~ dgamma(0.01, 0.01)
    # sd.beta0 <- sqrt(1/tau.beta0)

    # Slope paramters
    beta0 ~ dnorm(0, 0.01)
    beta1 ~ dnorm(0, 0.01)
    beta2 ~ dnorm(0, 0.01)
    beta3 ~ dnorm(0, 0.01)

    # Dirichlet prior for daily ODBA weights
    for(j in 1:days){
      delta[j] ~ dgamma(1,1)
    }
    
    for (j in 1:days) {
      weight[j] <- delta[j]/sumD
        for (i in 1:nind) {
      # For each time period into the past, compute weighted ODBA variable
        antX1[j,i] <- weight[j]*ptf[i,days-j+1]
        }
    # Reorder weights from recent to older
    weightOrdered[days-j+1] <- weight[j]
    }
    
    # Compute sum of deltas (unnormalized weights)
    sumD <- sum(delta)
    
    # Compute cumulative daily weights
    for (j in 1:days) {
    cum.weight[j] <- sum(weightOrdered[1:j])
    }
    
    # Compute antecedent PTF by summing weighted PTF variable over past days
    for (i in 1:nind) {
    antPTF[i] <- sum(antX1[,i])
    }
    
    ## Likelihood
    for (i in 1:nind) {
      defer[i] ~ dbern(p[i])
      logit(p[i]) <- beta0 + beta1*antPTF[i] + beta2*pop[i] + beta3*antPTF[i]*pop[i]
    }

    }
